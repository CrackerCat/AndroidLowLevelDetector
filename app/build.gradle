apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion androidBuild.compileSdkVersion
    buildToolsVersion androidBuild.buildToolsVersion

    defaultConfig {
        applicationId 'net.imknown.android.forefrontinfo'

        manifestPlaceholders = [appName: '@string/app_name']

        // setProperty('archivesBaseName', 'lld-$versionName')
        archivesBaseName = 'lld'

        versionCode androidBuild.versionCode
        versionName androidBuild.versionName

        minSdkVersion androidBuild.minSdkVersion
        targetSdkVersion androidBuild.targetSdkVersion

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        resConfigs 'zh-rCN', 'fr-rFR'
    }

    signingConfigs {
        release {
            storeFile file(keysConfig.release.storeFile)
            storePassword keysConfig.release.storePassword
            keyAlias keysConfig.release.keyAlias
            keyPassword keysConfig.release.keyPassword
        }

        debug {
            storeFile file(keysConfig.debug.storeFile)
            storePassword keysConfig.debug.storePassword
            keyAlias keysConfig.debug.keyAlias
            keyPassword keysConfig.debug.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true

            signingConfig signingConfigs.release

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro', 'proguard-gson-rules.pro'
        }

        debug {
            debuggable true
            jniDebuggable true

            signingConfig signingConfigs.debug

            applicationIdSuffix '.debug'
        }
    }

    def version = 'version'
    flavorDimensions version

    productFlavors {
        // stable
        master {
            dimension version
        }

        // beta
        develop {
            dimension version

            manifestPlaceholders = [appName: '@string/app_beta_name']

            versionCode androidBuild.versionBetaCode
            versionName androidBuild.versionBetaName
            versionNameSuffix "-beta$androidBuild.versionBetaIteration"
        }
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name != 'debug') {
            return
        }

        def appName
        def flavorInfix
        def flavorName = variant.getFlavorName()
        if (flavorName == 'master') {
            appName = '@string/app_debug_name'
            flavorInfix = ''
        } else if (flavorName == 'develop') {
            appName = '@string/app_beta_debug_name'
            flavorInfix = " ${capFirstLetter('beta')}"
        } else {
            appName = ''
            flavorInfix = ''
        }

        variant.getMergedFlavor().manifestPlaceholders = [appName: appName]
        resValue 'string', 'leak_canary_display_activity_label', "LLD${flavorInfix} Leaks"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // https://kotlinlang.org/docs/tutorials/android-plugin.html
    // androidExtensions {
    //     experimental = true
    // }
}

static def capFirstLetter(String flavor) {
    flavor.substring(0, 1).toUpperCase(Locale.US) + flavor.substring(1)
}

repositories {
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // region [Kotlin]
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinSdk.core"
    // implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinSdk.core"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinSdk.coroutines"
    // endregion [Kotlin]

    // region [AndroidX]
    implementation "com.google.android.material:material:$androidX.material"

    implementation "androidx.fragment:fragment-ktx:$androidX.fragment"
    implementation "androidx.preference:preference:$androidX.preference"
    implementation "androidx.webkit:webkit:$androidX.webkit"

    implementation "androidx.constraintlayout:constraintlayout:$androidX.constraintLayout"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$androidX.swipeRefreshLayout"
    implementation "androidx.recyclerview:recyclerview:$androidX.recyclerView"
    // endregion [AndroidX]

    // region [Test]
    testImplementation "junit:junit:$androidX.junit"
    androidTestImplementation "androidx.test.ext:junit:$androidX.extJunit"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidX.espressoCore"
    // endregion [Test]

    // region [3rd Parties]
    implementation "com.github.topjohnwu.libsu:core:$thirdParties.libsu"

    // debugImplementation "com.squareup.leakcanary:leakcanary-android:$thirdParties.leakCanary"
    debugImplementation "com.squareup.leakcanary:leakcanary-android-process:$thirdParties.leakCanary"

    implementation "com.google.code.gson:gson:$thirdParties.gson"

    implementation "com.github.kittinunf.fuel:fuel:$thirdParties.fuel"
    // implementation "com.github.kittinunf.fuel:fuel-gson:$thirdParties.fuel"
    // implementation "com.github.kittinunf.fuel:fuel-android:$thirdParties.fuel"
    implementation "com.github.kittinunf.fuel:fuel-coroutines:$thirdParties.fuel"
    // implementation "com.github.kittinunf.fuel:fuel-livedata:$thirdParties.fuel"

    implementation "com.g00fy2:versioncompare:$thirdParties.versionCompare"
    // endregion [3rd Parties]
}
