apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion androidBuild.compileSdkVersion
    buildToolsVersion androidBuild.buildToolsVersion

    defaultConfig {
        applicationId 'net.imknown.android.forefrontinfo'

        manifestPlaceholders = [appName: '@string/app_name']

        // setProperty('archivesBaseName', 'lld')
        archivesBaseName = 'lld'

        versionCode androidBuild.versionCode
        versionName androidBuild.versionName

        minSdkVersion androidBuild.minSdkVersion
        targetSdkVersion androidBuild.targetSdkVersion

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        resConfigs 'zh-rCN', 'fr-rFR'
    }

    signingConfigs {
        release {
            storeFile file(keysConfig.release.storeFile)
            storePassword keysConfig.release.storePassword
            keyAlias keysConfig.release.keyAlias
            keyPassword keysConfig.release.keyPassword
        }

        debug {
            storeFile file(keysConfig.debug.storeFile)
            storePassword keysConfig.debug.storePassword
            keyAlias keysConfig.debug.keyAlias
            keyPassword keysConfig.debug.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true

            signingConfig signingConfigs.release

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro', 'proguard-gson-rules.pro'
        }

        debug {
            debuggable true
            jniDebuggable true

            signingConfig signingConfigs.debug

            applicationIdSuffix '.debug'
        }
    }

    def version = 'version'
    flavorDimensions version

    productFlavors {
        // stable
        master {
            dimension version
        }

        // beta
        develop {
            dimension version

            manifestPlaceholders = [appName: '@string/app_beta_name']

            versionCode androidBuild.versionBetaCode
            versionName androidBuild.versionBetaName
            versionNameSuffix "-beta$androidBuild.versionBetaIteration"
        }
    }

    applicationVariants.all { variant ->
        if (buildType.name == 'release') {
            renameApk(variant)
        } else if (buildType.name == 'debug') {
            setAppName(variant)
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    // https://kotlinlang.org/docs/tutorials/android-plugin.html
    // androidExtensions {
    //     experimental = true
    // }
}

// region [Set AppName]
private static void setAppName(variant) {
    def appName
    def flavorInfix
    def flavorName = variant.flavorName
    if (flavorName == 'master') {
        appName = '@string/app_debug_name'
        flavorInfix = ''
    } else if (flavorName == 'develop') {
        appName = '@string/app_beta_debug_name'
        flavorInfix = " ${capFirstLetter('beta')}"
    } else {
        appName = ''
        flavorInfix = ''
    }

    variant.getMergedFlavor().manifestPlaceholders = [appName: appName]
    variant.resValue 'string', 'leak_canary_display_activity_label', "LLD${flavorInfix} Leaks"
}

private static def capFirstLetter(String flavor) {
    flavor.substring(0, 1).toUpperCase(Locale.US) + flavor.substring(1)
}
// endregion [Set AppName]

// region [Rename APK]
private def renameApk(variant) {
    def currentTaskNames = gradle.startParameter.taskNames

    def hasTaskName = currentTaskNames.size != 1
    if (hasTaskName) {
        return
    }

    def flavorName = variant.flavorName
    def buildTypeName = variant.buildType.name

    def currentTaskName = currentTaskNames[0]
    def isNotAssemble = !currentTaskName.equalsIgnoreCase("assemble$flavorName$buildTypeName")
    def isNotBundle = !currentTaskName.equalsIgnoreCase("bundle$flavorName$buildTypeName")
    if (isNotAssemble && isNotBundle) {
        return
    }

    def currentGitBranchName = getCurrentGitBranchName()
    if (!currentTaskName.toLowerCase(Locale.US).contains(currentGitBranchName)) {
        ant.fail("Task '$currentTaskName' should not use Git branch '$currentGitBranchName'.")
    }

    variant.outputs.all { output ->
        outputFileName = getRenamedApkName(variant)
        println "OutputFileName: $outputFileName"
    }
}

private def getRenamedApkName(variant) {
    def versionCode = variant.versionCode
    def versionName = variant.versionName
    def buildTypeName = variant.buildType.name
    def flavorName = variant.flavorName

    "${archivesBaseName}_${versionName}($versionCode)_${getCurrentDate()}_" +
            "$buildTypeName".charAt(0).toUpperCase() +
            "$flavorName".charAt(0).toUpperCase() +
            "$currentGitBranchName".charAt(0).toUpperCase() +
            ".apk"
}

import java.text.SimpleDateFormat

private def static getCurrentDate() {
    new SimpleDateFormat("yyyy-MM-dd-HH-mm", Locale.US).format(new Date())
}

private def static getCurrentGitBranchName() {
    def branch = ""
    def process = "git rev-parse --abbrev-ref HEAD".execute()
    process.in.eachLine { line -> branch = line }
    process.err.eachLine { line -> println line }
    process.waitFor()

    branch
}
// endregion [Rename APK]

apply from: "$rootDir/gradle/dependencies.gradle"
